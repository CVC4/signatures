; depends: nary_programs.plf

; make empty string of the given sort
(program sc_mk_emptystr ((s sort)) term
  (match s
    ((Seq t) (seq.empty s))
    (String emptystr)))

; note we must convert to original form recursively
(program sc_skolem_prefix ((s term) (n term)) term 
  (str.substr s (int 0) n)
)
(program sc_skolem_suffix_rem ((s term) (n term)) term
  (str.substr s n (a.- (str.len s) n))
)
(program sc_skolem_first_ctn_pre ((s term) (t term)) term (sc_skolem_prefix s (str.indexof s t (int 0))))
(program sc_skolem_first_ctn_post ((s term) (t term)) term (sc_skolem_suffix_rem s (a.+ (str.len (sc_skolem_first_ctn_pre s t)) (a.+ (str.len t) (int 0)))))

; head and tail for n-ary operators with null terminators
(program sc_string_head ((t term)) term (nary_head f_str.++ t))
(program sc_string_tail ((t term)) term (nary_tail f_str.++ t))

; TODO: these should case split on type to construct empty string

; decompose / concat
(program sc_string_concat ((t1 term) (t2 term)) term (nary_concat f_str.++ t1 t2 emptystr))
(program sc_string_decompose ((t term)) termPair (nary_decompose f_str.++ t emptystr))
(program sc_string_insert ((elem term) (t term)) term (nary_insert f_str.++ elem t emptystr))

; return reverse of t if rev = tt, return t unchanged otherwise
(program sc_string_rev ((t term) (rev flag)) term (ifequal rev tt (nary_reverse f_str.++ t emptystr) t))
(program sc_string_nary_elim ((t term)) term (nary_elim f_str.++ t emptystr))
(program sc_string_nary_intro ((t term)) term (nary_intro f_str.++ t emptystr))

; ========= Reduction for substring
; processing term:  substr( s, n, m )
; IF    n >=0 AND n < len( s ) AND m > 0
; THEN: s = k1 ++ k ++ k2 AND
;       len( k1 ) = n AND
;       ( len( k2 ) = len( s )-(n+m) OR len( k2 ) = 0 ) AND
;       len( k ) <= m
; ELSE: k = ""
(program sc_string_reduction_substr ((x term) (n term) (m term) (s sort)) term
  (let k (sc_mk_skolem (str.substr x n m))
  (let npm (a.+ n (a.+ m (int 0)))
  (let k1 (sc_mk_skolem (sc_skolem_prefix x n))
  (let k2 (sc_mk_skolem (sc_skolem_suffix_rem x npm))
  (ite
    ; condition
    (and (a.>= n (int 0))
      (and (a.> (str.len x) n)
        (and (a.> m (int 0))
          true)))
    ; if branch
    (and (= x (str.++ k1 (str.++ k (str.++ k2 (sc_mk_emptystr s)))))
      (and (= (str.len k1) n)
        (and (or (= (str.len k2) (a.- (str.len x) npm))
                (or (= (str.len k2) (int 0))
                  false))
          (and (a.<= (str.len k) m)
            true))))
    ; else branch
    (= k (sc_mk_emptystr s))
    )))))
)

;    // assert:
;    // IF:   ~contains( substr( x, n, len( x ) - n ), y ) OR n > len(x) OR 0 > n
;    // THEN: k = -1
;    // ELIF: y = ""
;    // THEN: k = n
;    // ELSE: substr( x, n, len( x ) - n ) = str.++( k1, y, k2 ) ^
;    //       ~contains( str.++( k1, substr( y, 0, len( y ) - 1) ), y ) ^
;    //       k = n + len( k1 )
;    // for k1, k2.
(program sc_string_reduction_indexof ((x term) (y term) (n term) (s sort)) term
  (let k (sc_mk_skolem (str.indexof x y n))
  (let xn (str.substr x n (a.- (str.len x) n))
  (let k1 (sc_mk_skolem (sc_skolem_first_ctn_pre xn y))
  (let k2 (sc_mk_skolem (sc_skolem_first_ctn_post xn y))
  (ite
    (or (not (str.contains xn y))
      (or (a.> n (str.len x))
        (or (a.> (int 0) n)
          false)))
    (= k (int (~ 1)))
    (ite
      (= y (sc_mk_emptystr s))
      (= k n)
      (and (= xn (str.++ k1 (str.++ y (str.++ k2 (sc_mk_emptystr s)))))
        (and (not (str.contains (str.++ k1 (str.++ (str.substr y (int 0) (a.- (str.len y) (int 1))) (sc_mk_emptystr s))) y))
          (and (= k (a.+ n (a.+ (str.len k1) (int 0))))
            true)))
)))))))

(program sc_string_reduction_pred ((t term) (s sort)) term
  (match t 
    ((apply t1 t2)
      (match t1
        ((apply t11 t12)
          (match t11
            ;(f_str.contains true)  ; TODO
            ((apply t111 t112) 
              (match t111
                (f_str.substr (sc_string_reduction_substr t112 t12 t2 s))
                (f_str.indexof (sc_string_reduction_indexof t112 t12 t2 s))
                ;(f_str.replace true)
            ; str.update
            ; str.from_int
            ; str.to_int
            ; seq.nth
            ; str.replaceall
            ; str.replace_re
            ; str.replace_re_all
            ; str.tolower
            ; str.toupper
            ; str.rev
            ; str.leq
))))))))

(program sc_string_reduction ((t term) (s sort)) term
  (and (sc_string_reduction_pred t s) (and (= t (sc_mk_skolem t)) true))
)

; ite( (str.contains t r), (= t (str.++ sk1 r sk2)), (not (= t r)))
(program sc_string_eager_reduction_contains ((t term) (r term) (s sort)) term
  (let k1 (sc_mk_skolem (sc_skolem_first_ctn_pre t r))
  (let k2 (sc_mk_skolem (sc_skolem_first_ctn_post t r))
  (ite
    (str.contains t r)
    (= t (str.++ k1 (str.++ r (str.++ k2 (sc_mk_emptystr s)))))
    (not (= t r)))
  ))
)

; ite( str.len(s)==1, 0 <= str.code(s) < |A|, str.code(s)=-1 )
(program sc_string_eager_reduction_to_code ((s term)) term
  (let t (str.to_code s)
  (ite
    (= (str.len s) (int 1))
    (and (a.>= t (int 0)) (and (a.< t (int 196608)) true))
    (= t (int (~ 1)))))
)

; (and (or (= (str.indexof x y n) (- 1)) (>= (str.indexof x y n) n)) (<= (str.indexof x y n) (str.len x)))
(program sc_string_eager_reduction_indexof ((x term) (y term) (n term)) term
  (let t (str.indexof x y n)
  (and (or (= t (int (~ 1))) (or (a.>= t n) false))
    (and (a.<= t (str.len x))
      true)))
)

(program sc_string_eager_reduction ((t term) (s sort)) term
  (match t 
    ((apply t1 t2)
      (match t1
        (f_str.to_code (sc_string_eager_reduction_to_code t2))
        ((apply t11 t12)
          (match t11
            (f_str.contains (sc_string_eager_reduction_contains t12 t2 s))
            ((apply t111 t112)
              (match t111
                (f_str.indexof (sc_string_eager_reduction_indexof t112 t12 t2)))))))))
)

(program sc_re_unfold_pos_concat ((t term) (r term) (ro term) (i mpz)) termPair
  (match r
    ((apply r1 r2)
      (match (sc_re_unfold_pos_concat t r2 ro (mp_add i 1))
        ((pair p1 p2)
        (let r12 (getarg f_re.++ r1)
        (let r122 (try_getarg f_str.to_re r12)
        (ifequal r122 r12 
          (let k (skolem_re_unfold_pos t ro i)
          (pair (str.++ k p1) (and (str.in_re k r12) p2)))
          (pair (str.++ r122 p1) p2)))))))
    (default (pair emptystr true))
))

(program sc_non_empty_concats ((t term)) term
  (match t
    ((apply t1 t2)
      (and (not (= (getarg f_str.++ t1) emptystr)) (sc_non_empty_concats t2)))
    (emptystr true)))

(program sc_re_unfold_pos ((t term) (r term)) term
  (match r
    ((apply r1 r2)
      (match r1
        ; case for star
        (f_re.*
          (let rr (re.++ r2 (re.++ r (re.++ r2 re.empty)))
          (match (sc_re_unfold_pos_concat t rr rr 0)
            ((pair p1 p2)
              (or (= t emptystr)
                (or (str.in_re t r2)
                  (or (and (sc_string_nary_elim (and (= t p1) p2)) (sc_non_empty_concats p1))
                    false)))))))
        ((apply r11 r12)
          (match r11
            ; case for concatenation
            (f_re.++
              (match (sc_re_unfold_pos_concat t r r 0)
                ((pair p1 p2) (sc_string_nary_elim (and (= t p1) p2)))))
))))))
